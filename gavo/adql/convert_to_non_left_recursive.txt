        <query_expression> ::=
                <non_join_query_expression>
              | <joined_table>

         <non_join_query_expression> ::=
                <non_join_query_term>
              | <query_expression> UNION  [ ALL ] <query_term>
              | <query_expression> EXCEPT [ ALL ] <query_term>

         <query_term> ::=
                <non_join_query_term>
              | <joined_table>

         <non_join_query_term> ::=
                <non_join_query_primary>
              | <query term> INTERSECT [ ALL ] <query_primary>

         <query_primary> ::=
                <non_join_query_primary>
              | <joined_table>

         <non_join_query_primary> ::=
                <query_specification>
              | <left_paren> <non_join_query_expression> <right_paren>

==============  eliminate non_join_query_term

<query_expression> ::=
       <non_join_query_expression>
     | <joined_table>

<non_join_query_expression> ::=
       <non_join_query_primary>
     | <query term> INTERSECT [ ALL ] <query_primary>
     | <query_expression> UNION  [ ALL ] <query_term>
     | <query_expression> EXCEPT [ ALL ] <query_term>

<query_term> ::=
       <non_join_query_primary>
     | <query term> INTERSECT [ ALL ] <query_primary>
     | <joined_table>

<query_primary> ::=
       <non_join_query_primary>
     | <joined_table>

<non_join_query_primary> ::=
       <query_specification>
     | <left_paren> <non_join_query_expression> <right_paren>


================= eliminate non_join_query_primary.

# query_specification is safe since it must start with a SELECT.
# possibly left-recursive rules are set off with an empty line

<query_expression> ::=
       <non_join_query_expression>
     | <joined_table>

<non_join_query_expression> ::=
       <query_specification>
     | <left_paren> <non_join_query_expression> <right_paren>

     | <query term> INTERSECT [ ALL ] <query_primary>
     | <query_expression> UNION  [ ALL ] <query_term>
     | <query_expression> EXCEPT [ ALL ] <query_term>

<query_term> ::=
       <query_specification>
     | <left_paren> <non_join_query_expression> <right_paren>

     | <query term> INTERSECT [ ALL ] <query_primary>
     | <joined_table>

<query_primary> ::=
       <query_specification>
     | <left_paren> <non_join_query_expression> <right_paren>
     | <joined_table>


================= eliminate query_term left-recursion

# NB. the first operand of query_term works out to be query_primary,
# hence no special query_term_op nonterminal.

<query_expression> ::=
       <non_join_query_expression>
     | <joined_table>

<non_join_query_expression> ::=
       <query_specification>
     | <left_paren> <non_join_query_expression> <right_paren>

     | <query term>
     | <query_expression> UNION  [ ALL ] <query_term>
     | <query_expression> EXCEPT [ ALL ] <query_term>

<query_term> ::=
     <query_primary> { INTERSECT [ ALL ] <query_primary> }

<query_primary> ::=
       <query_specification>
     | <left_paren> <non_join_query_expression> <right_paren>
     | <joined_table>


================== introduce additive_set_operator

# query primary isn't recursive, and hence query term isn't

<query_expression> ::=
       <joined_table>

     | <non_join_query_expression>

<additive_set_operator> ::= UNION  [ ALL ] | EXCEPT [ ALL ] 

<non_join_query_expression> ::=
       <query_specification>
     | <left_paren> <non_join_query_expression> <right_paren>
     | <query term>

     | <query_expression> <additive_set_operator> <query_term>

<query_term> ::=
     <query_primary> { INTERSECT [ ALL ] <query_primary> }

<query_primary> ::=
       <query_specification>
     | <left_paren> <non_join_query_expression> <right_paren>
     | <joined_table>


=================== introduce <non_join_query_expression_op>


<query_expression> ::=
       <joined_table>
     | <non_join_query_expression>

<additive_set_operator> ::= UNION  [ ALL ] | EXCEPT [ ALL ] 

<non_join_query_expression_op> ::=
       <query_specification>
     | <left_paren> <non_join_query_expression> <right_paren>
     | <query term>

<non_join_query_expression> ::=
       <non_join_query_expression_op>
     | <query_expression> <additive_set_operator> <query_term>

<query_term> ::=
     <query_primary> { INTERSECT [ ALL ] <query_primary> }

<query_primary> ::=
       <query_specification>
     | <left_paren> <non_join_query_expression> <right_paren>
     | <joined_table>


=================== eliminate non_join_query_expression left recurson

<query_expression_op> ::=
       <joined_table>
     | <non_join_query_expression_op>

<query_expression> ::=
     <query_expression_op> {<additive_set_operator> <query_term>}

<non_join_query_expression> ::=
       <non_join_query_expression_op>
     | <query_expression> <additive_set_operator> <query_term>

<additive_set_operator> ::= UNION  [ ALL ] | EXCEPT [ ALL ] 

<non_join_query_expression_op> ::=
       <query_specification>
     | <left_paren> <non_join_query_expression> <right_paren>
     | <query term>

<query_term> ::=
     <query_primary> { INTERSECT [ ALL ] <query_primary> }

<query_primary> ::=
       <query_specification>
     | <left_paren> <non_join_query_expression> <right_paren>
     | <joined_table>

